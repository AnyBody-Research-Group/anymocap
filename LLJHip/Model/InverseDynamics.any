

#if InverseDynamicModel

//This study is used for running the inverse dynamic analysis of the optimized motion
//***********************************************************************************
HumanModel={
  AnyFolder &Mannequin=.InverseDynamicStudy.Mannequin;
  #include  "BodyPartsSetup.any"
};
#include "HumanModel.any"

AnyBodyStudy InverseDynamicStudy = {
  AnyFolder &C3DData=..ModelSetup.C3DFileData ;
  AnyFolder &BodyModel=.HumanModel.BodyModel;
  #include "Mannequin.any" 
  
  //Environment around the human
  AnyFolder EnvironmentModel ={
    
    AnyFolder DrawC3DMarkers = {};
    //Model of the floor and force plates this is where the force plates are defined
    //        AnyFolder &HumanModelRef=..HumanModel.BodyModel;
    AnyFolder &BodyModelRef=.BodyModel;        
    
    //This environment file makes use of automatic detecteion of which foot are incontact with which plate 
    #if SetupFDA
    #include "EnvironmentAutoDetectionType2.any"
    #endif
    #if SetupABT
    #include "EnvironmentAutoDetectionType4.any"
    #endif
    //This environment file has no automatic detecteion of which foot are incontact with which plate it has to be set manually
    //#include "Environment.any"
  };
  
  //Connection between environment and the human    
  AnyFolder ModelEnvironmentConnection = {
    //Drivers for the model
    #include "JointsAndDriversOptimized.any"      
  };

  #include "HipExchange.any"
  
  #include "STL.any"
  
  #if fdk      
  #if contactHRF        
  #include "Contact.any"
  #include "VisualizationContact.any"
  #endif     
  #if analyticalHRF        
  #include "Analytical.any"
  #include "VisualizationAnalytical.any"
  #endif 
  #else
  #include "Visualization.any"
  #endif
  
  
  #if camera        
  #include "Cameras.any"
  #endif
  
  
  #if fdk       
  InverseDynamics.ForceDepKinOnOff=On;
  InverseDynamics.ForceDepKin.ForceTol=1;// 5e-1;
  InverseDynamics.ForceDepKin.Perturbation=5e-5;
  InverseDynamics.ForceDepKin.LocalSearchOnOff = On;
  InverseDynamics.ForceDepKin.MaxNewtonStep = 0.003;
  
  #endif 
  InverseDynamics.Criterion.UpperBoundOnOff=Off;
  
  #if SetupFDA
  Gravity ={0, 0, -9.81};
  nStep = Main.ModelSetup.nStep - Main.Parameters.TimeStepOffset;
  #endif
  #if SetupABT
  Gravity ={0, -9.81 , 0};
  nStep = Main.ModelSetup.nStep - Main.Parameters.TimeStepOffset;
  #endif
  
  tStart = Main.TrialSpecificData.tStart+2*Kinematics.ApproxVelAccPerturb; 
  tEnd = Main.TrialSpecificData.tEnd-2*Kinematics.ApproxVelAccPerturb;
  
};

//This study is only used for loading the optimized parameters  

AnyOptKinStudy  LoadParametersOptimizationResults =     {
  AnyFolder &Study  = .InverseDynamicStudy;
  AnyFolder  &HumanModel = .HumanModel;
  ParameterOptimization.ConvergenceTol = 1e-3;
  Analysis =   {
    AnyOperation  &ref = .Study.Kinematics;
  };
};

#endif



