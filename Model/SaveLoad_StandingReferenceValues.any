   
// Only include these oprations if the model uses a standing reference
// recording
 #ifdef MOCAP_IS_STANDING_REFERENCE 
   #if MOCAP_IS_STANDING_REFERENCE == 1
       #define _SAVE_STANDING_REFERNCE_PARAMETERS_
   #else 
       #define _LOAD_STANDING_REFERNCE_PARAMETERS_
   #endif
 #endif
 
 
    
    
#ifdef _SAVE_STANDING_REFERNCE_PARAMETERS_
  Main.ModelSetup.Macros = {        
   AnyOperationSequence Save_standing_reference_parameters = {
      AnyOperationSetValue TouchMarkers = {
        Source = ObjSearch("Main.ModelSetup.MocapDrivers.*.sRelOptEdit"     );
        Target = Source;
      };     
      AnyOperationSetValue TouchSegLength =  {
        Source = { 
          &Main.HumanModel.Scaling.Scaling.AnthroSegmentLengths.PelvisWidth,
          &Main.HumanModel.Scaling.Scaling.AnthroSegmentLengths.Right.ThighLength,
          &Main.HumanModel.Scaling.Scaling.AnthroSegmentLengths.Right.ShankLength,
          &Main.HumanModel.Scaling.Scaling.AnthroSegmentLengths.Right.FootLength,
          &Main.HumanModel.Scaling.Scaling.AnthroSegmentLengths.HeadHeight,
          &Main.HumanModel.Scaling.Scaling.AnthroSegmentLengths.TrunkHeight,
          &Main.HumanModel.Scaling.Scaling.AnthroSegmentLengths.UpperArmLength,
          &Main.HumanModel.Scaling.Scaling.AnthroSegmentLengths.LowerArmLength};
        Target = Source;
      };     
      AnyOperationMacro SaveToFile = {
        MacroStr  = {"classoperation Main" + strquote("Save Values") +"--file=" + 
          strquote( TEMP_PATH +"/"+ MOCAP_OUTPUT_FILENAME_PREFIX + Main.ModelSetup.TrialSpecificData.CalibrationTrialFileName+".anyset" )
        };
        
      };
    };
    AnyFolder &PostOperationRef  =  Main.RunApplication.PostOperations;
    PostOperationRef = {
      AnyOperation &Save_Standing_Reference_Parameters = .Save_standing_reference_parameters;
    };
  };
#endif
 
 
 
#ifdef _LOAD_STANDING_REFERNCE_PARAMETERS_
Main.ModelSetup.Macros = {        
  AnyOperationMacro Load_standing_reference_parameters = {
    MacroStr  = {"classoperation Main" + strquote("Load Values") +"--file=" +
      strquote(TEMP_PATH+"/"+MOCAP_OUTPUT_FILENAME_PREFIX + Main.ModelSetup.TrialSpecificData.CalibrationTrialFileName +".anyset" ),
      "classoperation Main" + strquote("Update Values")
    };
  };
  AnyFolder &PreOperationRef  =  Main.RunApplication.PreOperations;
  PreOperationRef = {
    AnyOperation &Load_standing_reference_parameters = .Load_standing_reference_parameters;
  };
};
#endif


