// Main.Studies:


AnyKinStudy BVH_CreateMarkerTrajectoriesStudy = 
{  
  #include "<BM_MANNEQUIN_FILE>"  
  
  AnyFolder MarkerOutput ={};
  
  AnyFolder &BVHData= Main.ModelSetup.BVHFileData ;
  
  //Environment around the human
  AnyFolder& EnvironmentModel =  Main.EnvironmentModel;
  
  Gravity = Main.ModelSetup.LabSpecificData.Gravity; 
  
  AnyIntVar FirstFrame = 0 ;
  AnyIntVar LastFrame  = Main.ModelSetup.BVHFileData.Header.FramesNo - 1  ;
  
  nStep  = LastFrame - FirstFrame + 1 ;
  tStart = FirstFrame * Main.ModelSetup.BVHFileData.Header.FrameTime ;
  tEnd   = LastFrame * Main.ModelSetup.BVHFileData.Header.FrameTime ;
  
  
//      nStep   = ..ModelSetup.TrialSpecificData.nStep ;
//      tStart = ..ModelSetup.TrialSpecificData.tStart ;
//      tEnd   = ..ModelSetup.TrialSpecificData.tEnd   ;      
};//BVH_CreateMarkerTrajectoriesStudy    
    


Main = 
{
  AnyOperationSequence BVH_PreProcessSequence = 
  {
    AnyOperation& BVH_ViewSetMacro = Main.ModelSetup.Views.SetViewMacros.BVHView;
    AnyOperation& BVH_CreateMarkerTrajectoriesStudy = Main.Studies.BVH_CreateMarkerTrajectoriesStudy.Kinematics;
    AnyOperation& BVH_SetValueOperation = Main.ModelSetup.Macros.BVH_Preprocess.BVH_SetValueOperation;
    AnyOperation& BVH_UpdateValuesOperation = Main.ModelSetup.Macros.BVH_Preprocess.BVH_UpdateValuesOperation;
  };
};


    
Main.ModelSetup.Macros = {
   AnyFolder BVH_Preprocess =
   {
   
    //Dummy study used for transfer of values
    AnyKinStudy OutputFunStudy = 
    {
      nStep = 1;
      Gravity = {0.0, -9.81, 0.0};  
    };  
    
    //Setvalue operations
    AnyOperationSequence BVH_SetValueOperation = 
    {
      AnyOperation& update_outputfuns = .OutputFunStudy.InitialConditions;
      AnyOperationSetValue copy = {
        Source = ObjSearch("..OutputFunStudy.*Val_*");
        Target = ObjSearch("Main.ModelSetup.C3DFileData.Points.Markers.*.FromBVH.val_*");
      };
    };//BVH_SetValueOperations     
    
    AnyOperationMacro BVH_UpdateValuesOperation =   
    {  
      MacroStr = ({ "classoperation Main" + strquote("Update Values") });  
    };//BVH_UpdateValuesOperation   
   
   };
};



Main.Studies.KinematicStudy = 
{
  AnyFolder& BVHFileData = Main.ModelSetup.BVHFileData;
};


Main.Studies.ParameterIdentification = 
{
  AnyDesVar DummyVar = 
  {
    Val = 0;
  };
};

Main.ModelSetup.Views = 
{
  DrawGroups = 
  {
    AnyDrawGroup All_BVH ={Objects = ObjSearchRecursive("Main.ModelSetup.BVHFileData","*", "AnyDrawObject"); };      
  };
  
  SetViewMacros = 
  {
    AnyOperationMacro BVHView = {
      MacroStr = {"classoperation "+ CompleteNameOf(&Main.ModelSetup.Views.BVHView) + strquote("Set View") };
    };       
  };
  
  AnyProjectModelViewDefinition BVHView =
  {
    DrawGroupSequence = {&.DrawGroups.All, &.DrawGroups.All_BVH, &.DrawGroups.All_Environment};
    Reset = {On, On, On};
    Hide =  {On, Off, Off}; 
    ShowModelDefined = {Off, Off, Off}; 
    ShowAutoGenerated = {Off, Off, Off}; 
  };      
};

