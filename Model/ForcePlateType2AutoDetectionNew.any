#import "AnyTest_Templates.any"

#class_template ForcePlateType2AutoDetection (PlateName,
                                              Folder,
                                              AnySeg &Limb1,
                                              AnySeg &Limb2, 
                                              No,
                                              VerticalDirection,
                                              HeightTolerance,
                                              VelThreshold,
                                              Fx,Fy,Fz,
                                              Mx,My,Mz,
                                              CREATE_KIN_GRF = 0 ,
                                              FootPresent = 1
                                              ){
  
  #var AnySwitch Switch_DrawForceVectorFromCOP = On;
  #var AnyVar MaxLoad = max( vnorm({Fx,Fy,Fz}') );

  
  #var AnyVec3 SurfaceOffset = {0,0,0};
  
  #var AnyVec3 SurfaceCorner_c01 = Corners.Origin + Corners.c01.sRel * Corners.Axes' + SurfaceOffset;
  #var AnyVec3 SurfaceCorner_c02 = Corners.Origin + Corners.c02.sRel * Corners.Axes' + SurfaceOffset;
  #var AnyVec3 SurfaceCorner_c03 = Corners.Origin + Corners.c03.sRel * Corners.Axes' + SurfaceOffset;
  #var AnyVec3 SurfaceCorner_c04 = Corners.Origin + Corners.c04.sRel * Corners.Axes' + SurfaceOffset;
  
  
  
  
  AnyIntVar Force_Plate_Type = Folder.Groups.FORCE_PLATFORM.TYPE.Data[No];
  ASSERT_EQ AssertForcePlateType (
   val1 = .Force_Plate_Type, 
   val2 = 2,
   Err = "Force-plate type appears to inconsistant with this force-plate template"
  ) = {};
    
  
  AnyFixedRefFrame Corners ={
    AnyVar CoordinateSystemSize= 0.1;
    
    AnyRefNode c01={
      AnyInt i=0;
      AnyInt plnr=No;
      sRel=Folder.PointsScaleFactor*{
        Folder.Groups.FORCE_PLATFORM.CORNERS.Data[plnr][i][0],
        Folder.Groups.FORCE_PLATFORM.CORNERS.Data[plnr][i][1],
        Folder.Groups.FORCE_PLATFORM.CORNERS.Data[plnr][i][2]
      };
      AnyDrawNode drw={ScaleXYZ=0.01*{1,1,1};RGB={1,0,0};};
      AnyDrawVector  DrawName = {
        Vec = {0.0,0,0};  //use zero length
        Line.Thickness = 0.025; //arbitary value
        Text = "1"; //make reference to name
        Line.RGB={1,0,0};//make reference to color
      };
    };
    
    AnyRefNode c02={
      AnyInt i=1;
      AnyInt plnr=No;
      sRel=Folder.PointsScaleFactor*
       {Folder.Groups.FORCE_PLATFORM.CORNERS.Data[plnr][i][0],
        Folder.Groups.FORCE_PLATFORM.CORNERS.Data[plnr][i][1],
        Folder.Groups.FORCE_PLATFORM.CORNERS.Data[plnr][i][2]
      };
      AnyDrawNode drw={ScaleXYZ=0.01*{1,1,1};RGB={1,0,0};};
      AnyDrawVector  DrawName = {
        Vec = {0.0,0,0};  //use zero length
        Line.Thickness = 0.025; //arbitary value
        Text = "2"; //make reference to name
        Line.RGB={1,0,0};//make reference to color
      };
    };
    
    AnyRefNode c03={
      AnyInt i=2;
      AnyInt plnr=No;
      sRel=Folder.PointsScaleFactor*
       {Folder.Groups.FORCE_PLATFORM.CORNERS.Data[plnr][i][0],
        Folder.Groups.FORCE_PLATFORM.CORNERS.Data[plnr][i][1],
        Folder.Groups.FORCE_PLATFORM.CORNERS.Data[plnr][i][2]};
      AnyDrawNode drw={ScaleXYZ=0.01*{1,1,1};RGB={1,0,0};};
      
      AnyDrawVector  DrawName = {
        Vec = {0.0,0,0};  //use zero length
        Line.Thickness = 0.025; //arbitary value
        Text = "3"; //make reference to name
        Line.RGB={1,0,0};//make reference to color
      };
    };
    
    AnyRefNode c04={
      AnyInt i=3;
      AnyInt plnr=No;
      sRel=Folder.PointsScaleFactor*
       {Folder.Groups.FORCE_PLATFORM.CORNERS.Data[plnr][i][0],
        Folder.Groups.FORCE_PLATFORM.CORNERS.Data[plnr][i][1],
        Folder.Groups.FORCE_PLATFORM.CORNERS.Data[plnr][i][2]};
      AnyDrawNode drw={ScaleXYZ=0.01*{1,1,1};RGB={1,0,0};};
      AnyDrawVector  DrawName = {
        Vec = {0.0,0,0};  //use zero length
        Line.Thickness = 0.025; //arbitary value
        Text = "4"; //make reference to name
        Line.RGB={1,0,0};//make reference to color
      };
      
    };
    
    AnyRefNode PlateCenter={
      sRel=0.25*(.c01.sRel+.c02.sRel+.c03.sRel+.c04.sRel);
      AnyVec3 p1=sRel;
      AnyVec3 p2=0.5*(.c01.sRel+.c04.sRel);
      AnyVec3 p3=0.5*(.c01.sRel+.c02.sRel);
      ARel =RotMat(p1,p2,p3);
      
    };
    
  };
  
  
  
  AnySeg ForcePlate={
    Mass=0.0;
    Jii={0,0,0};
    r0=.Corners.PlateCenter.sRel;
    
    //Rotational tranformation matrix for a coordinate system defined by three points. The first point is the origin of the system; the second gives the direction of the first (x) axis. Together all three points must span a plane which will be the plane of the first (x) and the second (y) axes, having the third (z) axis as normal. 
    AnyVec3 p1=.Corners.PlateCenter.sRel;
    AnyVec3 p2=0.5*(.Corners.c01.sRel+.Corners.c04.sRel);
    AnyVec3 p3=0.5*(.Corners.c01.sRel+.Corners.c02.sRel);
    Axes0 =RotMat(p1,p2,p3);
    
    AnyInt plnr=No;
    AnyFloat Origins= Folder.Groups.FORCE_PLATFORM.ORIGIN.Data;
    AnyVar z_check = iffun(gtfun(Origins[plnr][2], 0), 1, -1);
    AnyMessage Origin_Z_value_message= 
    {
      TriggerConst = iffun(gteqfun(.z_check,0), 1, 0);
      Type = MSG_Message ;
      Message = "The older AMTI origin has its Z value as positive. So this value is converted automatically. Please refer to www.c3d.org/HTML/type21.htm.";
    };
    //Transducer location      
    AnyRefNode TransducerLocation =
    {
      AnyFloat Origins= Folder.Groups.FORCE_PLATFORM.ORIGIN.Data;
      AnyVar xdist=.z_check*Origins[.plnr][0]*Folder.PointsScaleFactor;//distance from x axis on transducer to x axis of center of plate
      AnyVar ydist=.z_check*Origins[.plnr][1]*Folder.PointsScaleFactor; //distance from y axis on transducer to y axis of center of plate
      AnyVar zdist=.z_check*Origins[.plnr][2]*Folder.PointsScaleFactor;//distance from z axis on transducer to z surface of plate
      
      sRel={xdist,ydist,zdist};
//      AnyDrawRefFrame drw={ScaleXYZ=0.4*{1,1,1};RGB={1,0,0};};
    };
    
    AnyRefNode PlateSurface = {
      sRel = ( 0.25*(
               ..SurfaceCorner_c01 +
               ..SurfaceCorner_c02 +
               ..SurfaceCorner_c03 +
               ..SurfaceCorner_c04 ) - .r0)*.Axes0;
        
        
      AnyRefNode SurfaceGraphics = {
         sRel={0,0,0.03};
         AnyVec3 Size={
          vnorm( ...SurfaceCorner_c01 - ...SurfaceCorner_c02 ,2),
          vnorm( ...SurfaceCorner_c02 - ...SurfaceCorner_c03 ,2),
          0.06
      };

         AnyDrawSurf DrwSurfaceBox = 
         {
           FileName = "box";
           AnyVar LoadRatio = ....InContactOnOff * ....OnOff*vnorm(....Force.Flocal) / (....MaxLoad+ 0.001)  ;
           RGB = 0.45*{1,1,1} ;
           ScaleXYZ= .Size/0.30;
           Opacity =0.20+ ....InContactOnOff *....OnOff*(0.05+ 0.4* LoadRatio);
           Face=-1;
           Visible = On;
         };    
      };
    };

    
  };  
  
  AnyKinEqSimpleDriver ForcePlateDriver ={
    AnyKinLinear ForcePlateLin={
      AnyRefNode &ref1=..Corners.PlateCenter;
      AnySeg &ref2=..ForcePlate;
      Ref=0;
    };
    
    AnyKinRotational ForcePlateRot={
      AnyRefNode &ref1=..Corners.PlateCenter;
      AnySeg &ref2=..ForcePlate;
      Type=RotAxesAngles;
    };
    DriverPos={0,0,0,0,0,0}; 
    DriverVel={0,0,0,0,0,0};
    Reaction.Type = {Off,Off,Off,Off,Off,Off};
  };
  
  
  
  AnyFloatVar tStart = Folder.Header.FirstFrameNo/Folder.Header.VideoFrameRate;
  AnyFloatVar tEnd =  (Folder.Header.LastFrameNo+1)/Folder.Header.VideoFrameRate;
  AnyInt NoAnalogData= (Folder.Header.LastFrameNo - Folder.Header.FirstFrameNo +1)*Folder.Header.NoAnalogSamplesPer3DFrame;
  AnyFloat Time= farr( tStart, (tEnd-tStart)/( NoAnalogData - 1) ,  NoAnalogData);
  
  AnyFunInterpol load ={
    Type=PiecewiseLinear;
    T=.Time;
    Data={Fx,Fy,Fz,Mx,My,Mz};
  }; 
  
  
  AnyVar FzTotal=load(ForcePlateDriver.t)[2];
  AnyVar OnOff=iffun(gtfun(-FzTotal,10.0),1.0,0.0);
  AnyVar InContactOnOff = iffun( eqfun(0, ContactDetectionLimb2.NodeWithInBox.WithinBoxAndVelBelowThreshold +
                                          ContactDetectionLimb1.NodeWithInBox.WithinBoxAndVelBelowThreshold)
                                   , 0, 1);
  
  
  
  
  AnyForce3D Force ={
    
    AnyRefFrame  &ref=.ForcePlate.TransducerLocation ;
    Flocal= .OnOff*{.load(.ForcePlateDriver.t)[0],.load(.ForcePlateDriver.t)[1],.load(.ForcePlateDriver.t)[2]};
    AnyDrawVector DrawForce =     {
      AnyRefFrame &ref=.ref;
      Vec=.Flocal*1/1000;
      PointAway = Off;
      DrawCoord = Off;
      
      Line.RGB ={0,0,1};
      Line.Thickness = 0.01;
      Line.End.Thickness = 2*0.01;
      Line.End.Length = 4*0.01;
      GlobalCoord=Off;
    };
    
    
  };  
  
  
  AnyMoment3D Moment =
  {
    AnyRefFrame  &ref=.ForcePlate.TransducerLocation ;
    Mlocal=.OnOff*{.load(.ForcePlateDriver.t)[3],.load(.ForcePlateDriver.t)[4],.load(.ForcePlateDriver.t)[5]}*Folder.PointsScaleFactor;
    AnyDrawVector DrawMoment = 
    {
      AnyRefFrame &ref=.ref;
      Vec=.Mlocal*1/1000;
      PointAway = Off;
      DrawCoord = Off;
      Line.RGB ={0,1,1};
      Line.Thickness = 0.01;
      Line.End.Thickness = 2*0.01;
      Line.End.Length = 4*0.01;
      GlobalCoord=Off;
    };
  };  
  
  AnyFolder CenterOfPressure =
  {
     AnyForceMomentMeasure2 NetEffectMeasure = 
     {
       AnyRefFrame& ref = ..ForcePlate.PlateSurface;
       AnySeg& refseg = ..ForcePlate;
       AnyForceBase& Forces = ..Force;
       AnyForceBase& Moments = ..Moment;
       
       AnyVec3 Flocal = F*ref.Axes;
       AnyVec3 Mlocal = M*ref.Axes;
     };
     
     AnyVar fx = NetEffectMeasure.Flocal[0]; 
     AnyVar fy = NetEffectMeasure.Flocal[1]; 
     AnyVar fz = NetEffectMeasure.Flocal[2];
     AnyVar mx = NetEffectMeasure.Mlocal[0]; 
     AnyVar my = NetEffectMeasure.Mlocal[1]; 
     AnyVar mz = NetEffectMeasure.Mlocal[2]; 

     AnyVar fzz =iffun(gtfun(abs(fz),0),fz,fz+1000000);
     
     AnyVar Vx= -my/fzz;
     AnyVar Vy= mx/fzz;
     AnyVar Vz= 0;     
     
     AnyVar OnOff=.OnOff * .InContactOnOff ;
     
     

     AnyRefFrame& ref_ForcePlate = .ForcePlate.PlateSurface;
     ref_ForcePlate  = 
     {
       AnyDrawSphere COP_ball = 
       {
         RGB = {0,1,0};
         ScaleXYZ = 0.015 *{1,1,1};
         //Opacity = iffun(gtfun(..fz, -10.0), 0.0, 1.0);
         Opacity = ..OnOff;
         Position = {..Vx, ..Vy, ..Vz};
       };       
     };
     
     AnyDrawLine Line = 
     {
       p0 = {.Vx, .Vy, .Vz};
       p1 = p0+0.004*.OnOff*{.fx, .fy, .fz};
       Visible = ..Switch_DrawForceVectorFromCOP ;
       AnyRefFrame &ref = ..ForcePlate.PlateSurface ;
       
       Line.RGB ={0,0,1};
       Line.Thickness = 0.01;
       Line.End.Thickness = 2*0.01;
       Line.End.Length = 4*0.01;
       GlobalCoord=Off;
     };
     
  };  
  
  #if VerticalDirection=="X"
  AnyIntArray Index={1,2,0};
  #endif
  
  #if VerticalDirection=="Y" 
  AnyIntArray Index={0,2,1};
  #endif
  
  #if VerticalDirection=="Z" 
  AnyIntArray Index={0,1,2};
  #endif
  
  AnyFolder ContactDetectionLimb1 ={
    AnyVec3 P1= .Limb1.HeelContactNodeLow.r;
    AnyVar P1Vel= vnorm(.Limb1.HeelContactNodeLow.rDot,2);
    
    AnyVec3 P2= .Limb1.ToeLateralContactNode.r;
    AnyVar P2Vel= vnorm(.Limb1.HeelContactNodeLow.rDot,2);
    
    #include "ContactDetectionNew.any"
    
    AnyMuscleModelUsr1 InContactMuscle = {
      F0 = 0;
      S = .NodeWithInBox.WithinBoxAndVelBelowThreshold *10000;
    };
    
    AnyMuscleModelUsr1 NoContactMuscle = {
      AnyVar Limb2Contact=iffun(eqfun(..ContactDetectionLimb2.NodeWithInBox.WithinBoxAndVelBelowThreshold ,1.0),0.0,1.0); //equal 0 if limb2 in contact
      S =Limb2Contact*.NodeWithInBox.OutsideBoxOrVelHigherThanThreshold*10000;
      F0 = 0;
    };
    
    #if FootPresent
    
    AnyFolder PlateFootReactions = {
      AnySeg &ref1=..ForcePlate;
      AnySeg &ref2=..Limb1;
      AnyMuscleModel &MusMdl=.InContactMuscle;
      #include "<ANYBODY_PATH_TOOLBOX>/Mocap/ArtificialMuscleConnection.any"
    };
    #endif
    
    AnyFolder PlateGroundReactions={
      AnyRefFrame &ref1=..Corners.PlateCenter;
      AnySeg &ref2=..ForcePlate;
      AnyMuscleModel &MusMdl=.NoContactMuscle;
      #include "<ANYBODY_PATH_TOOLBOX>/Mocap/ArtificialMuscleConnection.any"
    };
  };
 
  AnyFolder ContactDetectionLimb2 = {
    AnyVec3 P1= .Limb2.HeelContactNodeLow.r;
    AnyVar P1Vel= vnorm(.Limb2.HeelContactNodeLow.rDot,2);
    
    AnyVec3 P2= .Limb2.ToeLateralContactNode.r;
    AnyVar P2Vel= vnorm(.Limb2.HeelContactNodeLow.rDot,2);
    
    #include "ContactDetectionNew.any"
    
    AnyMuscleModelUsr1 InContactMuscle = {
      F0 = 0;
      S = .NodeWithInBox.WithinBoxAndVelBelowThreshold *10000;
    };
    
    AnyMuscleModelUsr1 NoContactMuscle = {
      AnyVar Limb1Contact=iffun(eqfun(..ContactDetectionLimb1.NodeWithInBox.WithinBoxAndVelBelowThreshold ,1.0),0.0,1.0); //equal 0 if limb1 in contact
      S =Limb1Contact*.NodeWithInBox.OutsideBoxOrVelHigherThanThreshold*10000;
      F0 = 0;
    };
    
    #if FootPresent
    
    AnyFolder PlateFootReactions={
      AnySeg &ref1=..ForcePlate;
      AnySeg &ref2=..Limb2;
      AnyMuscleModel &MusMdl=.InContactMuscle;
      #include "<ANYBODY_PATH_TOOLBOX>/Mocap/ArtificialMuscleConnection.any"
    };
    
    #endif
    
    AnyFolder PlateGroundReactions={
      AnyRefFrame &ref1=..Corners.PlateCenter;
      AnySeg &ref2=..ForcePlate;
      AnyMuscleModel &MusMdl=.NoContactMuscle;
      #include "<ANYBODY_PATH_TOOLBOX>/Mocap/ArtificialMuscleConnection.any"
    };
  };
  
#if CREATE_KIN_GRF == 1
  
  ForcePlate =
  {
    AnyRefNode KinGRF_rotnode = { ARel = RotMat(pi,y); };
  };
  
  AnyFolder Kinematic_GRF = 
  {
    
    AnyFloat weightdata = sqrt(Calculations.norm);
    AnyVar max_val = max({ max(weightdata),0.000001});
    AnyVar Th = 0.05*max_val;
    
    // Thresshold the weight data
    AnyVector ThresHolded = iffun(gtfun(weightdata, Th ),
                             (weightdata-Th )/(max_val-Th),
                             0*farr(0.0,1.0,NumElemOf(weightdata)));

    AnyFunInterpol GlobalCOP_pos = 
    {
      Type = Bspline;
      T = ..Time;
      Data = ((1+0*farr(0.0,1.0,..NoAnalogData))'*{..ForcePlate.r0}+ (..ForcePlate.Axes0*.Calculations.GRF_start.Data)')';
    };    

    
    
    // Alternative Time array which starts from tStart and ends at tEnd  
    AnyInt NoAnalogData2= (Folder.Header.LastFrameNo - Folder.Header.FirstFrameNo)*Folder.Header.NoAnalogSamplesPer3DFrame;
    AnyFloat tStart2 = .tStart;
    AnyFloat tEnd2 = .tEnd-1/Folder.Header.VideoFrameRate;
    AnyFloat Time2 = farr( tStart2, (tEnd2-tStart2)/( NoAnalogData2 - 1) ,  NoAnalogData2);

    AnyFloat limb1_COP_dist = vnorm(Main.ModelSetup.C3DFileData.Points.Markers.R_CALC.PosInterpol(Time2) - 
                                    GlobalCOP_pos(Time2));
    AnyFloat limb2_COP_dist = vnorm(Main.ModelSetup.C3DFileData.Points.Markers.L_CALC.PosInterpol(Time2)- 
                                    GlobalCOP_pos(Time2));

                                   
    AnyFunInterpol Weight_limb1 = 
    {
       AnyVar Factor = DesignVar(1);
       Type = Bspline;
       T = .Time2;
       Data = {Factor * iffun(ltfun(.limb1_COP_dist,.limb2_COP_dist ), .ThresHolded, 0.0*.ThresHolded)};
    };

    AnyFunInterpol Weight_limb2 = 
    {
       AnyVar Factor = DesignVar(1);
       Type = Bspline;
       T = .Time2;
       Data = {Factor * iffun(ltfun(.limb2_COP_dist,.limb1_COP_dist ), .ThresHolded, 0.0*.ThresHolded)};
    };


    
    
    AnySeg Frame = 
    {
      Mass = 0.0;
      Jii = {0.0, 0.0, 0.0};
      AnyDrawVector arrow = {Vec ={0,0,..Calculations.GRF_normalized(.t)[0]};GlobalCoord = Off;Line.Thickness = 0.01;};
      AnyDrawRefFrame drw = { ScaleXYZ = 0.2*{1,1,1};};
      AnyRefNode vector= {sRel = {0,0,1};};
    };
    
    AnyKinEq lock_z_rot= {
      AnyKinMeasureOrg zrot = {
        AnyKinRotational rot = {
          Type= RotVector;
          AnyRefFrame &ref0 = ....ForcePlate.KinGRF_rotnode;
          AnyRefFrame &ref1 = ...Frame;
        };
        MeasureOrganizer = {2};
      };
    };
    
    AnyFolder Calculations = {
      
      AnyVector T =  ..load.T;
      AnyVector fx = ..load.Data[0]; 
      AnyVector fy = ..load.Data[1]; 
      AnyVector fz = ..load.Data[2];
      AnyVector mx =  (1/1000)*..load.Data[3]; 
      AnyVector my =  (1/1000)*..load.Data[4]; 
      AnyVector mz =  (1/1000)*..load.Data[5]; 
      
      AnyVector norm = vnorm({fx,fy,fz}');
      
      // Values used when dividing. To avoid deviding by zero. 
      AnyVar force_thresshold = 0.05*max(abs(fz));
      AnyVector _fz = iffun(gtfun(abs(fz),force_thresshold ),fz, fz+10^10);
      AnyVector _norm = iffun(gtfun(abs(norm),force_thresshold ),norm, norm+10^10) ;

      
      AnyVar originz = -1*..ForcePlate.TransducerLocation.zdist;
      
      
      AnyVector Vx= (originz*fx-my)/( _fz ) ;
      AnyVector Vy= (mx+originz*fy)/( _fz );
      AnyVector Vz= 0*Vy;
      
      AnyFunInterpol GRF_normalized = 
      {
        Type = PiecewiseLinear;
        T = .T;
        Data = {.norm/( iffun(gtfun(max(.norm),0),max(.norm), 10^10) )};
      };
      
      AnyFunInterpol GRF_start = 
      { 
        Type = PiecewiseLinear;
        T = .T;
        Data = {.Vx, .Vy, .Vz};
      };    
      AnyFunInterpol GRF_end = 
      { 
        Type = PiecewiseLinear;
        T = .T;
        
        Data = {.Vx + .fx/(._norm),
          .Vy + .fy/(._norm)
          //.COP_cal.Vz + .COP_cal.fz/ (.COP_cal.norm)
        };      
      };
      
      
    };//KinGRF_Calculations 
    
    
    AnyKinDriver COPDriver = {
      AnyKinLinear start_lin = {
        AnyRefFrame &ref0 = ...ForcePlate.PlateSurface;
        AnyRefFrame &ref1 = ..Frame;
        Ref = 0;
      };
      AnyParamFun &fun = .Calculations.GRF_start;
      CType = {Hard,Hard,Hard};
    };
    
    AnyKinDriver DirectionDriver = {
      AnyKinMeasureOrg end_lin_xy = {
        AnyKinLinear lin = 
        {
          AnyRefFrame &ref0 = ....ForcePlate.PlateSurface;
          AnyRefFrame &ref1 = ...Frame.vector;
          Ref = 0;
        };
        MeasureOrganizer = {0,1};
      };
      AnyParamFun &fun = .Calculations.GRF_end ;
      CType = {Hard, Hard};
    };  
  } ;
  #endif
  
  
};


