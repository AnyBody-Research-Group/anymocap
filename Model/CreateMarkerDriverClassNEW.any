#class_template CreateMarkerDriver (MarkerName,
                                    MarkerPlacement,
                                    WeightX=1, WeightY=1, WeightZ=1,
                                    OptX="Off", OptY = "Off",OptZ="Off",
                                    sRelOptScalingOnOff = "On",
                                    FilterCutOffFrequency = 0,
                                    FilterOrder = 2,
                                    UseC3DWeightResiduals = "Off",
                                    PARAMETER_OPT_STUDY = ParameterIdentification,
                                    C3D_OBJECT = C3DFileData,
                                    BODY_MODEL_FOLDER = BodyModel
                                    ){
                                      
  #var AnyVar ConstMarkerWeightX = WeightX;
  #var AnyVar ConstMarkerWeightY = WeightY;
  #var AnyVar ConstMarkerWeightZ = WeightZ;
   
  AnyFunConst FunConstMarkerWeights ={ Value = {.ConstMarkerWeightX , .ConstMarkerWeightY , .ConstMarkerWeightZ };};

  #if UseC3DWeightResiduals == "On"
    #var AnyObjectPtrVar MarkerWeightsPtr = &Main.ModelSetup.C3D_OBJECT.Points.Markers.MarkerName.Weight;
  #else
    #var AnyObjectPtrVar MarkerWeightsPtr = &FunConstMarkerWeights;
  #endif
  
  AnyVec3 RGB={1,0,0};  //color of initial markers

    
  #var AnyVec3 sRelOpt ;  // Variable with obligatory initialization
  #var AnyVec3 sRelOptDelta = {0, 0, 0};
  
  AnyFolder &Macro=  Main.ModelSetup.Macros ;
  Macro={
    AnyStringVar Set##MarkerName = ("classoperation "+  CompleteNameOf(.sRelOptEdit) + " " + strquote("Set Value") +  strformat ("\n")+ "classoperation Main " + strquote("Update Values") + strformat ("\n"));
  };
  
  #if OptX =="Off" 
  
    #if OptY=="On" 
      #if OptZ=="On" 
        AnyVec3 sRelOptEdit = (sRelOptDelta);
      #endif      
    
      #if OptZ=="Off" 
        AnyVec3 sRelOptEdit = (sRelOptDelta);
      #endif      
    #endif        
  

    #if OptY=="Off" 
      #if OptZ=="On" 
        AnyVec3 sRelOptEdit = (sRelOptDelta);
      #endif      
    
      #if OptZ=="Off" 
        AnyVec3 sRelOptEdit = DesignVar(sRelOptDelta); //make it a design var ! because it will not be otherwise
      #endif      
    #endif        
  

 #endif
              
  
  #if OptX =="On" 
  
    #if OptY=="On" 
      #if OptZ=="On" 
        AnyVec3 sRelOptEdit = (sRelOptDelta); 
      #endif      
    
      #if OptZ=="Off" 
        AnyVec3 sRelOptEdit = (sRelOptDelta);
      #endif      
    #endif        
  

    #if OptY=="Off" 
      #if OptZ=="On" 
        AnyVec3 sRelOptEdit = (sRelOptDelta);
      #endif      
    
      #if OptZ=="Off" 
        AnyVec3 sRelOptEdit = (sRelOptDelta);
      #endif      
    #endif        
  
 
 #endif  
  
  AnyFolder &MarkerInsertionNode = Main.HumanModel.BODY_MODEL_FOLDER.MarkerPlacement;
  MarkerInsertionNode = {
    
    AnyRefNode MarkerName={
      #if sRelOptScalingOnOff == "On"
        sRel= .AnatomicalFrame.sRel+ (.GeomScale(..sRelOpt) + ..sRelOptEdit )*.AnatomicalFrame.ARel' ;
      #else
        sRel= .AnatomicalFrame.sRel+ (..sRelOpt + ..sRelOptEdit )*.AnatomicalFrame.ARel' ;
      #endif
      ARel= .AnatomicalFrame.ARel;
      
      
      AnyDrawVector XDir =   {
        #if OptX =="On" 
        Line.RGB={0,1,0};
        #endif
        #if OptX =="Off" 
        Line.RGB={1,0,0};
        #endif
        Vec = {size,0, 0};
        #include "DrawVector.any"
        Text="X";
      };
      
      AnyDrawVector YDir =   {
        #if OptY =="On" 
        Line.RGB={0,1,0};
        #endif
        #if OptY =="Off" 
        Line.RGB={1,0,0};
        #endif
        Vec = {0,size, 0};
        #include "DrawVector.any"
        Text="Y";
      };
      
      AnyDrawVector ZDir =   {
        #if OptZ =="On" 
        Line.RGB={0,1,0};
        #endif
        #if OptZ =="Off" 
        Line.RGB={1,0,0};
        #endif
        Vec = {0,0,size};
        #include "DrawVector.any"
        Text="Z";
      };

   
      #if OptX =="On" 
      AnyDesVar DesVarX = {
        Val = ...sRelOptEdit[0];    
        Min=-100;
        Max=100;      
      };
      #endif
      
      #if OptY =="On"
      AnyDesVar DesVarY = {
        Val = ...sRelOptEdit[1];   
        Min=-100;
        Max=100;      
      };
      #endif
      
      #if OptZ =="On"
      AnyDesVar DesVarZ = {
        Val = ...sRelOptEdit[2];   
        Min=-100;
        Max=100;      
      };
      #endif    
      
      
    }; //create the marker 
  };
  
  
  
//  Main.ModelSetup.MOCAP_DRIVER_FOLDER ={
//        
//    AnyFolder MarkerName ={
      
      AnyKinDriverMarker Driver = {
        
        
        
        
        AnyDrawKinMeasure Draw = {
          Visible = Off;
          Opacity = 1;
          Label = Off;
          Size = 0.01;
          Line = On;
        };
        Linear.Ref=0;
        AnyRefFrame &MarkerRef = Main.HumanModel.BODY_MODEL_FOLDER.MarkerPlacement.MarkerName;
        
        #if FilterCutOffFrequency > 0
   	  /// A lowpass butterworth filter
   	    AnyFunButterworthFilter LowPassFilter = 
   	    {
   	     	FilterForwardBackwardOnOff = On;
   	     	AutomaticInitialConditionOnOff = On;
   	     	N = FilterOrder;
            W = {1/(Main.ModelSetup.C3D_OBJECT.Filter.Fs[0]*0.5)*FilterCutOffFrequency };
   	      Type = LowPass;
   	    };
  	  
          // Filter the data
          AnyMatrix FilteredPos = LowPassFilter(Main.ModelSetup.C3D_OBJECT.Points.Markers.MarkerName.Pos');
          
          /// Interpolation function of the lowpass filtered data
          AnyFunInterpol FilteredPosInterpol = 
          {
            Type = PiecewiseLinear;
            T = Main.ModelSetup.C3D_OBJECT.Points.Markers.MarkerName.PosInterpol.T;
            Data = .FilteredPos;
          };
          
        #else
        
        AnyParamFun &InterpolRef = Main.ModelSetup.C3D_OBJECT.Points.Markers.MarkerName.PosInterpol;
        #endif
        
        
        WeightFun={.MarkerWeightsPtr};
      };
//    };
//    
//  };  

  Main.Studies.PARAMETER_OPT_STUDY ={

    #if OptX =="On" 
    AnyDesVar &MarkerName##X =  Main.HumanModel.BODY_MODEL_FOLDER.MarkerPlacement.MarkerName.DesVarX ;
    #endif
    
    #if OptY =="On"
    AnyDesVar &MarkerName##Y =  Main.HumanModel.BODY_MODEL_FOLDER.MarkerPlacement.MarkerName.DesVarY ;
    #endif
    
    #if OptZ =="On"
    AnyDesVar &MarkerName##Z =  Main.HumanModel.BODY_MODEL_FOLDER.MarkerPlacement.MarkerName.DesVarZ ;
    #endif        
    
    
    
  };
  
  
  
  
  
  
}; // End of InsertSegmentClass






