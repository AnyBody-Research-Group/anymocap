
#ifndef MOCAP_LAB_SPECIFIC_DATA
  #path MOCAP_LAB_SPECIFIC_DATA "ExampleFiles/LabSpecificData.any"
#endif

#ifndef MOCAP_SUBJECT_SPECIFIC_DATA
  #path MOCAP_SUBJECT_SPECIFIC_DATA "ExampleFiles/SubjectSpecificData.any"
#endif

#ifndef MOCAP_TRIAL_SPECIFIC_DATA
  #path MOCAP_TRIAL_SPECIFIC_DATA "ExampleFiles/TrialSpecificData.any"
#endif



AnyFolder ModelSetup = {
    // The folder Lab/Subject/TrialSpecificData folders are
    // created using macros. This ensures that any required 
    // variables get a default value if they are not specified
    // by the user.
  
    CreateLabFolder LabSpecificData = {
      #include "<MOCAP_LAB_SPECIFIC_DATA>"
    };
    
    CreateSubjectFolder SubjectSpecificData = {
      #include "<MOCAP_SUBJECT_SPECIFIC_DATA>"
    };
    
    CreateTrialFolder TrialSpecificData = {
        #include "<MOCAP_TRIAL_SPECIFIC_DATA>"
    };
    
   // This file checks if all the defines statements 
   // are made correctly.
   #include "CheckMocapConfiguration.any"

  
  // Study for saving a loading the parameters from calibration trials 
  //(dynamic or standing refereces)
  #include "SaveLoad_StandingReferenceValues.any"

  #include "SaveLoadParameters.any"
  
    
   // This is the C3D file used for the analysis
  AnyInputC3D C3DFileData =   {
    #include "C3DSettings.any"
  };

  // Start and End times for the c3d file.
  AnyIntVar FirstFrame = C3DFileData.Header.FirstFrameNo + TrialSpecificData.C3DFirstFrameOffset ; 
  AnyIntVar LastFrame = C3DFileData.Header.LastFrameNo - TrialSpecificData.C3DLastFrameOffset ;
  AnyFloatVar tStart = FirstFrame/C3DFileData.Header.VideoFrameRate;
  AnyFloatVar tEnd = LastFrame/C3DFileData.Header.VideoFrameRate;
  
  AnyIntVar nStep =(LastFrame-FirstFrame+1);
  
  AnyFolder MocapDrivers = {
    
    #ifdef MOCAP_MARKER_PROTOCOL_FILE
    #include "<MOCAP_MARKER_PROTOCOL_FILE>"
    #endif
    
    #ifdef MOCAP_MARKER_PROTOCOL_FILE_1
    #include "<MOCAP_MARKER_PROTOCOL_FILE_1>"
    #endif
    
  };
  
  AnyFolder Measures = {
  };
  

  
  //markers adds macros automatically into this folder
  AnyFolder Macros =  {};
  
};


