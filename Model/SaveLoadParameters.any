  Main.ModelSetup.Macros = {        
   AnyOperationSequence Save_parameters= {
      AnyOperationSetValue TouchMarkers = {
        Source = ObjSearch("Main.ModelSetup.MocapDrivers.*.sRelOptEdit"     );
        Target = Source;
      };     
      AnyOperationSetValue TouchSegLength =  {
        Source = { 
          &Main.HumanModel.Scaling.Scaling.AnthroSegmentLengths.PelvisWidth,
          &Main.HumanModel.Scaling.Scaling.AnthroSegmentLengths.Right.ThighLength,
          &Main.HumanModel.Scaling.Scaling.AnthroSegmentLengths.Right.ShankLength,
          &Main.HumanModel.Scaling.Scaling.AnthroSegmentLengths.Right.FootLength,
          &Main.HumanModel.Scaling.Scaling.AnthroSegmentLengths.NeckLength,
          &Main.HumanModel.Scaling.Scaling.AnthroSegmentLengths.HeadHeight,
          &Main.HumanModel.Scaling.Scaling.AnthroSegmentLengths.TrunkHeight,
          &Main.HumanModel.Scaling.Scaling.AnthroSegmentLengths.TrunkWidth ,
          &Main.HumanModel.Scaling.Scaling.AnthroSegmentLengths.UpperArmLength,
          &Main.HumanModel.Scaling.Scaling.AnthroSegmentLengths.LowerArmLength,
          &Main.HumanModel.Scaling.Scaling.AnthroSegmentLengths.HandLength
        };
        Target = Source;
      };     
      AnyOperationMacro SaveToFile = {
        AnyFileVar SavePath = TEMP_PATH;
        MacroStr  = {"classoperation Main" + strquote("Save Values") +"--file=" + 
          strquote(FilePathCompleteOf(SavePath) +"/"+ MOCAP_OUTPUT_FILENAME_PREFIX + Main.ModelSetup.TrialSpecificData.TrialFileName +".anyset" )
        };
        
      };
    };
    Main.RunApplication.SaveParameters = {
      AnyOperation &Save_parameters_to_anyset = Main.ModelSetup.Macros.Save_parameters;
    };
  };
 
 
Main.ModelSetup.Macros = {
  
  #define _GET_VALID_INDEX(X) iffun(gtfun(NumElemOf(Main.ModelSetup.TrialSpecificData.LoadParametersFrom), X), X, 0)
  #define _LOAD_MACRO(X) iffun( gtfun(NumElemOf(Main.ModelSetup.TrialSpecificData.LoadParametersFrom), X) ,\
                                  "classoperation Main " + strquote("Load Values") +" --file=" + strquote( FilePathCompleteOf(LoadPath)+ "/" +MOCAP_OUTPUT_FILENAME_PREFIX + Main.ModelSetup.TrialSpecificData.LoadParametersFrom[ _GET_VALID_INDEX(X)]+".anyset") ,\
                                  "")
 
  AnyOperationMacro Load_parameters = {
    AnyFileVar LoadPath = TEMP_PATH;
    MacroStr  = { _LOAD_MACRO(0),
                  _LOAD_MACRO(1),
                  _LOAD_MACRO(2),
                  _LOAD_MACRO(3),
                  _LOAD_MACRO(4),
                  _LOAD_MACRO(5),
                  _LOAD_MACRO(6),
                  _LOAD_MACRO(7),
                  _LOAD_MACRO(8),
                  "classoperation Main " + strquote("Update Values")
                };   
  };
  #undef _LOAD_MACRO
  #undef _GET_VALID_INDEX
  Main.RunApplication.LoadParameters = {
    AnyOperation &Load_parameters_from_anyset = Main.ModelSetup.Macros.Load_parameters;
  };
};


