#include "PathDefinitions.any"
#include "ClassTemplates.any"

Main = 
{
  // Mocap related setup
  #include "ModelSetup.any"
  
  // Include environment model
  // e.g. global reference, forceplates etc.
  #include "EnvironmentModel.any"
  
  // Configure and include the human model
  #include "<BODY_MODEL_CONFIG_FILE>"
  #include "<ANYBODY_PATH_BODY>/HumanModel.any"
  
  
  AnyFolder Studies = 
  {
    // Study for parameter identification of segment length and marker positions. 
    AnyOptKinStudy ParameterIdentification = 
    {
      #include "KinematicStudyForParameterIdentification.any"  
      ParameterOptimization.ConvergenceTol=1e-4; 
    };

    // Overdeterminate kinematics study to generate joint angles for inv. dyn. study. 
    AnyKinStudy KinematicStudy = 
    { 
      // HACK_WORKAROUND We need to include a special body model with no muscles.
      #include "BodyModel_no_muscle_hack.any" 
      AnyFolder &C3DData= Main.ModelSetup.C3DFileData ;
      AnyFolder &EnvironmentModel = Main.EnvironmentModel;
      AnyFolder ModelEnvironmentConnection =
      {
        AnyFolder &MarkerDrivers = Main.ModelSetup.MocapDrivers;
        AnyFolder &ExtraDrivers = Main.ModelSetup.MocapExtraDrivers;
      }; 
      
      // HACK WORKAROUND. It is necessary write joint angles to a file. 
      #include "JointAngleOutputs.any"
      #include "KinematicStudySettings.any"
      nStep= Main.ModelSetup.TrialSpecificData.nStep; 
      Gravity = Main.ModelSetup.LabSpecificData.Gravity;
      tStart = Main.ModelSetup.TrialSpecificData.tStart; 
      tEnd = Main.ModelSetup.TrialSpecificData.tEnd;
    };
    
    
    // Study for running the inverse dynamic simulation
    AnyBodyStudy InverseDynamicStudy = 
    { 
      AnyFolder &C3DData= Main.ModelSetup.C3DFileData ;
      AnyFolder &BodyModel= Main.HumanModel.BodyModel;
      AnyFolder &EnvironmentModel = Main.EnvironmentModel;
      AnyFolder ModelEnvironmentConnection =
      {
        #include "JointsAndDriversOptimized.any"
      }; 
      // Include markers for visualization, but exclude the driver part 
      AnyFolder &Markers= Main.ModelSetup.MocapDrivers;
      AnyMechObjectExcluder Exclude={ Objects = ObjSearch("Main.ModelSetup.MocapDrivers.*.Driver");};
      
      
      #include "InverseDynamicSettings.any"
      #include "CollectedOutputs.any"
      

      // HACK WORKAROUND. Since it is necessary to add/subs ApproxVelAccPerturb from tStart/tEnd,
      // we make nStep four frames shorter, so steps are still evaluated on the actual frames. 
      tStart = Main.ModelSetup.TrialSpecificData.tStart+2*Kinematics.ApproxVelAccPerturb ; 
      tEnd = Main.ModelSetup.TrialSpecificData.tEnd-2*Kinematics.ApproxVelAccPerturb ;
      nStep= max({1, Main.ModelSetup.TrialSpecificData.nStep-4});    
      Gravity = Main.ModelSetup.LabSpecificData.Gravity;
      
      #include "StudyMuscleContributions.any"
    };
    
  };  //InverseDynamicStudy 
  
  // Macros to excute the model
  #include "RunApplication.any"  
}; //Main


