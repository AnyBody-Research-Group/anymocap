Main.ModelSetup.TrialSpecificData = 
{
  // This is the name of the trial c3d file without extension
  TrialFileName = "Plug-in-gait";
  
  // This the C3D frame where the analysis starts
  // If not specified it defaults to the first frame of the C3D file
  //FirstFrame = .C3DFileData.Header.FirstFrameNo;
  
  // This the C3D frame where the analysis ends
  // If not specified it defaults the first frame of the C3D file
  //LastFrame = .C3DFileData.Header.LastFrameNo;
  
  // tStart specifies the time of the analysis. Setting this overrides 
  // the 'FirstFrame' value
  //tStart = ;
  
  // tEnd specifies the tEnd time of the analysis. Setting this overrides 
  // the 'LastFrame' value
  //tEnd = ;
  
  // nStep specifies the number of steps used in the analysis. 
  // If not specified it defaults to: 1+(tEnd-tStart)*C3D_video_frame_rate
  //nStep = ;
  
  
  // The switch MOCAP_C3D_DATA_PATH can be used to specify the directory
  // where the C3D file is stored. Defaults to the main file directory.
  //#path MOCAP_C3D_DATA_PATH "<ANYBODY_PATH_MAINFILEDIR>"
  
  // The switch MOCAP_OUTPUT_FILENAME_PREFIX can be used to specify
  // a prefix to all output files. Useful in some cases
  // to differentiate trials. Defautls to nothing ""
  //#define MOCAP_OUTPUT_FILENAME_PREFIX ""
  
  
  
  /// The following settings define the initial posture of the
  /// model (e.g. at load time)
  InitialPositionOfBody= {
  
  
    // An alternative way to specify the initial pelvis position/rotation is to 
    // construct the position vector and rotation matrix from the values in the C3D file.
    AnyVec3 Pelvis_RASIS = Main.ModelSetup.C3DFileData.Points.Markers.RASI.PosInterpol(Main.ModelSetup.TrialSpecificData.tStart);
    AnyVec3 Pelvis_LASIS = Main.ModelSetup.C3DFileData.Points.Markers.LASI.PosInterpol(Main.ModelSetup.TrialSpecificData.tStart);
    AnyVec3 Pelvis_SACRUM = 0.5*( Main.ModelSetup.C3DFileData.Points.Markers.RPSI.PosInterpol(Main.ModelSetup.TrialSpecificData.tStart)
                                 +Main.ModelSetup.C3DFileData.Points.Markers.LPSI.PosInterpol(Main.ModelSetup.TrialSpecificData.tStart));
    
                                  
    PelvisRotationMatrix = RotMat(Pelvis_SACRUM , 
                                  0.5*(Pelvis_LASIS +Pelvis_RASIS ),
                                  Pelvis_LASIS )
                           * RotMat(pi/2,x);                              
    PelvisPos = (1/3)*( Pelvis_RASIS + Pelvis_LASIS + Pelvis_SACRUM );
    
      
    PelvisThoraxExtension = 0;
    PelvisThoraxLateralBending = 0;
    PelvisThoraxRotation = 0;
    
    NeckExtension = 0;
    NeckLateralBending = 0;
    NeckRotation = 0;
  
    Right = {
    
      HipFlexion = 26.54894;
      HipAbduction = 8.313687;
      HipExternalRotation = 0;
      KneeFlexion = -2.131877;
      AnklePlantarFlexion = 2.90032;
      SubTalarEversion = 0;
    
      GlenohumeralFlexion = 0;
      GlenohumeralAbduction = 0;
      GlenohumeralExternalRotation = 0;
      ElbowFlexion = 0;
      ElbowPronation = 0;
      WristFlexion = 0;
      WristAbduction = 0;
    
  };
    Left = {
    
      HipFlexion = -24.09937;
      HipAbduction = 0.6660672;
      HipExternalRotation = 0;
      KneeFlexion = -0.1293966;
      AnklePlantarFlexion = -9.499957;
      SubTalarEversion = 0;
    
      GlenohumeralFlexion = 0;
      GlenohumeralAbduction = 0;
      GlenohumeralExternalRotation = 0;
      ElbowFlexion = 0;
      ElbowPronation = 0;
      WristFlexion = 0;
      WristAbduction = 0;
    
  };
  
  
  }; // InitialPositionOfBody

};
